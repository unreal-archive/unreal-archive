subprojects {
	group = 'org.unrealarchive'
	version = "1.24"
	if (System.getenv().containsKey("BUILD_NUMBER")) {
		version += ".${System.env.BUILD_NUMBER}"
	} else version += ".DEV"

	apply plugin: 'java'
	apply plugin: 'maven-publish'
	apply plugin: 'jacoco'

	compileJava {
		options.release = 21
	}

	repositories {
		mavenCentral()
		maven {
			url = "https://git.pookaroo.au/api/packages/shrimp/maven"
			credentials(HttpHeaderCredentials) {
				name = "Authorization"
				value = "token ${providers.gradleProperty("packagesReadToken").get()}"
			}
			authentication {
				header(HttpHeaderAuthentication)
			}
		}
	}

	publishing {
		repositories {
			maven {
				name = "ShrimpWorks"
				url = 'https://git.pookaroo.au/api/packages/shrimp/maven'
				credentials(HttpHeaderCredentials) {
					name = "Authorization"
					value = "token ${System.env.PACKAGES_TOKEN}"
				}

				authentication {
					header(HttpHeaderAuthentication)
				}
			}
		}
		publications {
			maven(MavenPublication) {
				from components.java
			}
		}
	}

	/**
	 * Conditionally publish jlink'd binary if present in subproject
	 */
	afterEvaluate {
		def tarTask = tasks.findByName('jlinkTar')
		if (tarTask) {
			publishing.publications.maven.artifact(tarTask) {
				classifier = 'bin'
				extension = 'tgz'
			}
		}
	}

	dependencies {
		testRuntimeOnly('org.junit.platform:junit-platform-launcher')
	}

	test {
		useJUnitPlatform()
	}

	jacocoTestReport {
		reports {
			xml.required = true
		}
	}
}
