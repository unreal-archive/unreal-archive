subprojects {
	group = 'org.unrealarchive'
	version = "1.21"
	if (System.getenv().containsKey("BUILD_NUMBER")) {
		version += ".${System.env.BUILD_NUMBER}"
	} else version += ".DEV"

	apply plugin: 'java'
	apply plugin: 'maven-publish'
	apply plugin: 'jacoco'

	compileJava {
		options.release = 21
	}

	repositories {
		mavenCentral()
		maven {
			url = "https://code.shrimpworks.za.net/artefacts"
		}
	}

	publishing {
		repositories {
			maven {
				name = "ShrimpWorks"
				url = 'https://code.shrimpworks.za.net/artefacts/'
				authentication {
					basic(BasicAuthentication)
				}
				credentials(PasswordCredentials) {
					username = "${System.env.ARTEFACTS_USER}"
					password = "${System.env.ARTEFACTS_PASSWORD}"
				}
			}
		}

		publications {
			maven(MavenPublication) {
				from components.java
			}
		}
	}

	dependencies {
		testRuntimeOnly('org.junit.platform:junit-platform-launcher')
	}

	test {
		useJUnitPlatform()
	}

	jacocoTestReport {
		reports {
			xml.required = true
		}
	}
}
